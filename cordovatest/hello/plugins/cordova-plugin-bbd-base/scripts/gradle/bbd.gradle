/*
 (c) 2019 BlackBerry Limited. All rights reserved.
*/

String composeDefaultMavenUrl() {
    String sdkDir = getAndroidSdkDir()
    String[] mavenExtraPaths = ['extras', 'blackberry', 'dynamics_sdk', 'm2repository']
    sdkDir + File.separator + mavenExtraPaths.join(File.separator) + File.separator
}

String getLocalSdkDir() {
    def localProperties = new File(rootDir, "local.properties")
    if (!localProperties.exists()) {
        return null
    }

    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
        properties.load(instr)
    }

    properties.getProperty('sdk.dir')
}

String getAndroidSdkDir() {
    String androidSdkDir = getLocalSdkDir()
    if (androidSdkDir == null) {
        androidSdkDir = System.getenv('ANDROID_HOME')
        String androidSDKRootDir = System.getenv('ANDROID_SDK_ROOT')

        if (androidSdkDir == null && androidSDKRootDir != null) {
            androidSdkDir = androidSDKRootDir
        }
    }

    println('Android SDK location: ' + androidSdkDir)
    androidSdkDir
}

def getGradleProperties() {
    String[] propertiesExtraPaths = ['app', 'src', 'main', 'gradle.properties']
    def gradleProperties = new File(rootDir, propertiesExtraPaths.join(File.separator))
    if (!gradleProperties.exists()) {
        return null
    }

    Properties properties = new Properties()
    gradleProperties.withInputStream { instr ->
        properties.load(instr)
    }

    properties
}

String getBbdMavenLocation() {
    String mavenLocation
    def gradleProperties = getGradleProperties()
    mavenLocation = gradleProperties != null ? gradleProperties.getProperty('bbdSdkPath') : null
    if (mavenLocation == null) {
        mavenLocation = composeDefaultMavenUrl()
    }

    println('BlackBerry Dynamics m2repository location: ' + mavenLocation)
    mavenLocation
}

allprojects {
    repositories {
        google()
        jcenter()

        maven {
            url getBbdMavenLocation()
        }
    }
}

dependencies {
    implementation 'com.blackberry.blackberrydynamics:android_handheld_platform:+'
    implementation 'com.blackberry.blackberrydynamics:android_handheld_backup_support:+'
}
